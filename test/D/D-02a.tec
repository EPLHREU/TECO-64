! TECO test: Delete characters !
! Commands: :D !
! Requirements: None !
! Execution: Standard !
! Expect: PASS !

! Include: setup-01.tec !

@I/abcdefghijklmnopqrstuvwxyz 0123456789/ 10@I// Z UA 0 UD 0J

0:D MU                                   ! Test: 0:D !

::@S/abcde/ MU
QA-Z-QD MN

:D MU                                    ! Test: :D !

1%D ^[ R
::@S/egh/ MU
QA-Z-QD MN

1:D MU                                   ! Test: 1:D !

1%D ^[ R
::@S/hjk/ MU
QA-Z-QD MN

4:D MU                                   ! Test: n:D !

4%D ^[ R
::@S/kpq/ MU
QA-Z-QD MN

-:D MU                                   ! Test: -:D !

%D ^[ R
::@S/prs/ MU
QA-Z-QD MN

-1:D MU                                  ! Test: -1:D !

%D ^[ R
::@S/rtu/ MU
QA-Z-QD MN

-2:D MU                                  ! Test: -n:D !

2%D ^[ R
::@S/rvw/ MU
QA-Z-QD MN

HK @I/abcdefghijklmnopqrstuvwxyz 0123456789/ 10@I// Z UA 0J

28,31:D MU                               ! Test: m,n:D !

R ::@S/0456/ MU
QA-Z-3 MN

! Include: cleanup-01.tec !
