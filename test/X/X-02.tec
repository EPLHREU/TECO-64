! TECO test: Append to Q-register !
! Commands: X !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [X-02.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

0UA

10 <
    @I/line /
    %A \
    @I/ abcdefghijklmnopqrstuvwxyz 0123456789/
    GY
>

@^A/-----/ MZ
0J 1:XA :GA                         ! Test: 1:X !
@^A/-----/ MZ
2L 2:XA :GA                         ! Test: 2:X !
@^A/-----/ MZ
3L 3:XA :GA                         ! Test: 3:X !
@^A/-----/ MZ
ZJ -3:XA :GA                        ! Test: -3:X !
@^A/-----/ MZ
-4L -2:XA :GA                       ! Test: -2:X !
@^A/-----/ MZ
-3L -1:XA :GA                       ! Test: -1:X !
@^A/-----/ MZ

@^A/!PASS!/ MZ

EK HK EX
