! TECO test: Copy to Q-register !
! Commands: X !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [X-01.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

HK

0UA

10 <
    @I/line /
    %A \
    @I/ abcdefghijklmnopqrstuvwxyz 0123456789/
    13@I// 10@I//
>

@^A/-----/ MZ
0J 1XA :GA                          ! Test: 1X !
@^A/-----/ MZ
2L 2XA :GA                          ! Test: 2X !
@^A/-----/ MZ
3L 3XA :GA                          ! Test: 3X !
@^A/-----/ MZ
ZJ -3XA :GA                         ! Test: 4X !
@^A/-----/ MZ
-4L -2XA :GA                        ! Test: -2X !
@^A/-----/ MZ
-3L -1XA :GA                        ! Test: -1X !
@^A/-----/ MZ
HXA :GA                             ! Test: HX !
@^A/-----/ MZ

@^A/!PASS!/ MZ

EK HK EX
