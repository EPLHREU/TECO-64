!! TECO-64 test script: Test for symbol constituent -- "C
!! Expect: PASS

0 UC            !! Start at NUL

^^$ < %C-1 "C :@^A/!FAIL!/ ^C ' >         !! NUL to $

E1&4 "E                                 !! $ is conditionally a symbol chr.
    %C-1 "C :@^A/!FAIL!/ ^C '
|
    %C-1 "C | :@^A/!FAIL!/ ^C '
'

^^.-^^$-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! $ to .

%C-1 "C | :@^A/!FAIL!/ ^C '               !! . is always a symbol chr.

^^0-^^.-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! . to 0

^^9-^^0+1 < %C-1 "C | :@^A/!FAIL!/ ^C ' > !! Digits

^^A-^^9-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! Digits to upper case

^^Z-^^A+1 < %C-1 "C | :@^A/!FAIL!/ ^C ' > !! Upper case

^^_-^^Z-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! Upper case to _

E1&8 "E                                 !! _ is conditionally a symbol chr.
    %C-1 "C :@^A/!FAIL!/ ^C '
|
    %C-1 "C | :@^A/!FAIL!/ ^C '
'

^^a-^^_-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! _ to lower case

^^z-^^a+1 < %C-1 "C | :@^A/!FAIL!/ ^C ' > !! Lower case

255-^^z-1 < %C-1 "C :@^A/!FAIL!/ ^C ' >   !! Remaining 8-bit characters

0 UC            !! Start at NUL

^^$ < %C-1 "c :@^A/!FAIL!/ ^C ' >         !! NUL to $

E1&4 "E                                 !! $ is conditionally a symbol chr.
    %C-1 "c :@^A/!FAIL!/ ^C '
|
    %C-1 "c | :@^A/!FAIL!/ ^C '
'

^^.-^^$-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! $ to .

%C-1 "c | :@^A/!FAIL!/ ^C '               !! . is always a symbol chr.

^^0-^^.-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! . to 0

^^9-^^0+1 < %C-1 "c | :@^A/!FAIL!/ ^C ' > !! Digits

^^A-^^9-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! Digits to upper case

^^Z-^^A+1 < %C-1 "c | :@^A/!FAIL!/ ^C ' > !! Upper case

^^_-^^Z-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! Upper case to _

E1&8 "E                                 !! _ is conditionally a symbol chr.
    %C-1 "c :@^A/!FAIL!/ ^C '
|
    %C-1 "c | :@^A/!FAIL!/ ^C '
'

^^a-^^_-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! _ to lower case

^^z-^^a+1 < %C-1 "c | :@^A/!FAIL!/ ^C ' > !! Lower case

255-^^z-1 < %C-1 "c :@^A/!FAIL!/ ^C ' >   !! Remaining 8-bit characters

:@^A/!PASS!/

^C