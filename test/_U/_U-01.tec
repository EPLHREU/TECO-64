! TECO test: Load global Q-registers w/ text !
! Commands: ^U !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [_U-01.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

! Test: load global Q-registers w/ text !

0 U.A
10 <
    @I"@^U"
    ^^0 + Q.A I
    @I"{@^A/Loaded Q-register "
    ^^0 + Q.A I
    @I"/ M.Z}"
    GY
    %.A
>

0 U.A
26 <
    @I"@^U"
    ^^A + Q.A @I//
    @I"{@^A/Loaded Q-register "
    ^^A + Q.A I
    @I"/ M.Z}"
    GY
    %.A
>

HX.A HK M.A

0 U.B
10 <
    @I/:M/
    ^^0 + Q.B I
    @I/ /
    %.B
>

0 U.B
26 <
    @I/:M/
    ^^A + Q.B I
    @I/ /
    %.B
>

HX.B HK M.B

@^A/!PASS!/ M.Z

EK HK EX
