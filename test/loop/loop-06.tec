! TECO test: Test nested loops !
! Commands: <, > !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [loop-06.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        3 UZ                        ! CR/LF, -P !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 UZ                        ! LF, no -P !
    |                               ! TECO-64 !
        7 UZ                        ! LF, -P, ext. features !
    '
'

QZ&1 "N
    13@^UY//
    @^UZ/ 13^T/
'

10:@^UY//
:@^UZ/ 10^T/

0 U1

! Test: nested loops w/ different loop counts !

2 < @^A/1st loop: #/ %1=
    0 U2
    3 < @^A/2nd loop: #/ %2=
        0 U3
        4 <
            @^A/3rd loop: #/ %3=
        >
        @^A/finished 3rd loop/ MZ
    >
    @^A/finished 2nd loop/ MZ
>
@^A/finished 1st loop/ MZ

@^A/!PASS!/ MZ

EK HK EX
