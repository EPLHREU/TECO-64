! TECO test: Check size of text in Q-registers !
! Commands: ^Z !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [_Z-01.log] !

! Include: setup-01.tec !

(:QY+:QZ+:Q.Z) U9

@^A/^Z = / ^Z-Q9 := ^Z-Q9            ! Test: ^Z with Q-registers empty !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UA/abcdefghijklmnopwrstuvwxyz/

@^A/^Z = / ^Z-Q9 := ^Z-Q9-26         ! Test: ^Z w/ text in 1 Q-register !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UB/0123456789/

@^A/^Z = / ^Z-Q9 := ^Z-Q9-36         ! Test: ^Z w/ text in 2 Q-registers !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UC/now is the time for all good men to come to the aid of their party/

@^A/^Z = / ^Z-Q9 := ^Z-Q9-102        ! Test: ^Z w/ text in 3 Q-registers !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

! Include: cleanup-01.tec !
