! TECO test: Check size of text in Q-registers !
! Commands: ^Z !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [_Z-01.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

@^A/^Z = / ^Z-QZ := ^Z-QZ            ! Test: ^Z with Q-registers empty !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UA/abcdefghijklmnopwrstuvwxyz/

@^A/^Z = / ^Z-QZ := ^Z-QZ-26         ! Test: ^Z w/ text in 1 Q-register !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UB/0123456789/

@^A/^Z = / ^Z-QZ := ^Z-QZ-36         ! Test: ^Z w/ text in 2 Q-registers !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^UC/now is the time for all good men to come to the aid of their party/

@^A/^Z = / ^Z-QZ := ^Z-QZ-102        ! Test: ^Z w/ text in 3 Q-registers !
"E @^A/ (pass)/ | @^A/ (fail)/ ' MZ

@^A/!PASS!/ MZ

EK HK EX
