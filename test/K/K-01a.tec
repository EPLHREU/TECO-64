! TECO test: Delete lines !
! Commands: K !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [K-01a.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

0UA

10 <
    @I/line /
    %A \
    @I/ abcdefghijklmnopqrstuvwxyz 0123456789/
    GY
>

0J

@^A/-----/ MZ

HT

@^A/-----/ MZ

K HT                                ! Test: K !

@^A/-----/ MZ

1K HT                               ! Test: 1K !

@^A/-----/ MZ

3L 2K HT                            ! Test: nK !

@^A/-----/ MZ

-K HT                               ! Test: -K !

@^A/-----/ MZ

-1K HT                              ! Test: -1K !

ZJ

@^A/-----/ MZ

-2K HT                              ! Test: -nK !

HK

10 <
    @I/line /
    %A \
    @I/ abcdefghijklmnopqrstuvwxyz 0123456789/
    13@I// 10@I//
>

0J

@^A/-----/ MZ

5L 10K HT                           ! Test: nK !

@^A/-----/ MZ

-10K HT                             ! Test: -nK !

HK

@I/abcdefghijklmnopqrstuvwxyz 0123456789/ 13@I// 10@I//

10,20K HT                           ! Test: m,nK !

@^A/-----/ MZ

@^A/!PASS!/ MZ

EK HK EX
