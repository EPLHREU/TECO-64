! TECO test: Switch output streams !
! Commands: EA, EW` !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [EA-01.log] !

0 U.Z

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        1 U.Z                       ! -P !
        @^U.Z/ 13^T 10^T/           ! Set up macro to print CR/LF !
        13@^UY// 10:@^UY//          ! Set up macro to insert CR/LF !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 U.Z                       ! No -P !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    |                               ! TECO-64 !
        3 U.Z                       ! -P, ext. features !
        @^U.Z/ 10^T/                ! Set up macro to print LF !
        10@^UY//                    ! Set up macro to insert LF !
    '
'

@EZ{rm /tmp/foo; rm /tmp/baz}       ! Delete any existing files !

HK

@I/abcdef/ 10I                      ! Add some data !

. UA

@I/123456/ 10I                      ! Add some more data !

:@EW{/tmp/foo} "U ::^C '            ! Create 1st temp file !

EA                                  ! Test: switch to secondary output stream !

:@EW{/tmp/baz} "U ::^C '            ! Create 2nd temp file !

2 < B,QA PW >                       ! Write data to secondary stream !

@EW//                               ! Switch back to primary output stream !

3 < QA,Z PW>                        ! Write data to primary stream !

HT
HK

EC                                  ! Close primary stream !

EA                                  ! Switch back to secondary output stream !

EC                                  ! Close secondary stream !

:@ER{/tmp/foo} "U ::^C ' Y          ! Open 1st temp file and read it in !

@^A/-----/ M.Z

HT
HK

EC

:@ER{/tmp/baz} "U ::^C ' Y          ! Open 2nd temp file and read it in !

@^A/-----/ M.Z

HT

EC

@^A/!PASS!/ M.Z

EK HK EX
