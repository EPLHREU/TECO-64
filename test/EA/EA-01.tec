! TECO test: Switch output streams !
! Commands: EA, EW` !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [EA-01.log] !

! Include: setup-01.tec !

@I/abcdef/ 10I                      ! Add some data !

. UA

@I/123456/ 10I                      ! Add some more data !

:@EW{/tmp/TECO-01.lis} "U ::^C '    ! Create 1st temp file !

EA                                  ! Test: switch to secondary output stream !

:@EW{/tmp/TECO-02.lis} "U ::^C '    ! Create 2nd temp file !

2 < B,QA PW >                       ! Write data to secondary stream !

@EW//                               ! Switch back to primary output stream !

3 < QA,Z PW>                        ! Write data to primary stream !

HT
HK

EC                                  ! Close primary stream !

EA                                  ! Switch back to secondary output stream !

EC                                  ! Close secondary stream !

:@ER{/tmp/TECO-01.lis} "U ::^C ' Y  ! Open 1st temp file and read it in !

@^A/-----/ MZ

HT
HK

EC

:@ER{/tmp/TECO-02.lis} "U ::^C ' Y  ! Open 2nd temp file and read it in !

@^A/-----/ MZ

HT

EC

! Include: cleanup-01.tec !
