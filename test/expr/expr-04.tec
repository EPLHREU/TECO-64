!! TECO-64 test script: Test extended operators
!! Commands: (misc.)
!! Expect: PASS

0 UF            !! Count of failures

!! Macro to evaluate expression and compare with what we expected

@^UB
{
    UA                              !! Expected value
    :MA
    UB                              !! Calculated value
    QA-QB "E
        QB @=/^EQA = %6d (pass)/
    |
        QB @=/^EQA = %6d (fail)/
        %F
    '
}

@^UA{ (16 == 3) }      0 :MB        !! Compare equal
@^UA{ (3 == 16) }      0 :MB        !! Compare equal

@^UA{ (16 <> 3) }     -1 :MB        !! Compare not equal
@^UA{ (3 <> 16) }     -1 :MB        !! Compare not equal

@^UA{ (16 < 3)  }      0 :MB        !! Compare less than
@^UA{ (3 < 16)  }     -1 :MB        !! Compare less than

@^UA{ (16 <= 3) }      0 :MB        !! Compare less than or equal
@^UA{ (3 <= 16) }     -1 :MB        !! Compare less than or equal

@^UA{ (16 > 3)  }     -1 :MB        !! Compare greater than
@^UA{ (3 > 16)  }      0 :MB        !! Compare greater than

@^UA{ (16 >= 3) }     -1 :MB        !! Compare greater than or equal
@^UA{ (3 >= 16) }      0 :MB        !! Compare greater than or equal

@^UA{ (16 // 3) }      1 :MB        !! Division, yielding remainder
@^UA{ (3 // 16) }      3 :MB        !! Division, yielding remainder

@^UA{ (!0)      }     -1 :MB        !! Logical NOT
@^UA{ (!1)      }      0 :MB        !! Logical NOT
@^UA{ (!!0)     }      0 :MB        !! Logical NOT
@^UA{ (!!1)     }     -1 :MB        !! Logical NOT

@^UA{ (16 ~ 3)  }     19 :MB        !! Logical XOR
@^UA{ (3 ~ 16)  }     19 :MB        !! Logical XOR

@^UA{ (16 << 3) }    128 :MB        !! Left shift
@^UA{ (3 << 16) } 196608 :MB        !! Left shift

@^UA{ (16 >> 3) }      2 :MB        !! Right shift
@^UA{ (3 >> 16) }      0 :MB        !! Right shift

QF "N
    ::^C
'

:@^A/!PASS!/
