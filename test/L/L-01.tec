! TECO test: Get relative line info !
! Commands: :L !
! Requirements: None !
! Execution: Standard !
! Expect: PASS [L-01.log] !

EO - 100 "L
    -1 EJ - 1024 "E                 ! TECO-32 !
        3 UZ                        ! CR/LF, -P !
    |                               ! Unknown or unsupported TECO !
        @^A/Unknown TECO: EO=/
        EO :=
        @^A/, -1EJ=/
        -1EJ =
        ^C
    '
|
    EO - 200 "L                     ! TECO C !
        0 UZ                        ! LF, no -P !
    |                               ! TECO-64 !
        7 UZ                        ! LF, -P, ext. features !
    '
'

QZ&1 "N
    13@^UY//
    @^UZ/ 13^T/
'

10:@^UY//
:@^UZ/ 10^T/

HK

@^U.A
{
    U.A Q.A :@=/%3d line/ Q.A-1 "N @^A/s/ | @^A/ / '
}

@^U.B
{
     . :@=/.=%-3d, /
     0:L M.A @^A/ in page, /        ! Test:  0:L !
    -1:L M.A @^A/ preceding dot, /  ! Test: -1:L !
     1:L M.A @^A/ following dot/    ! Test:  1:L !
    MZ
}

10 <
    @I/abcdefghijklmnopwrstuvwxyz 0123456789/ 10 I
>

0J  M.B
 L  M.B
3L  M.B
3L  M.B
-5L M.B
ZL  M.B
-L  M.B
-ZL M.B

@^A/!PASS!/ MZ

EK HK EX
