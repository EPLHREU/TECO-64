! COLLECTION OF MACROS TO TURN BLISS COMMENTS TO LOWER CASE. MACRO M IS THE
  MAIN, WHICH PROCESSES THE BOILER PLATE AUTOMATICALLY AND THEN PASSES CONTROL
  TO THE INTERACTIVE PROCESSING.
!
Comment character?  ^T^UK 

!MACRO L: TURN COMMENT FIELD OF LINE TO LOWER CASE!
@^UL? 0L1^Q,:S^EQK"S MW"S <0AU1 Q1-13"E 0;'
  Q1"A @^UX%FS^EX% Q1#32:^UX27:^UX MX | C'> 0LS^EQK''?
!MACRO W: FIND START OF NEXT WORD ON THIS LINE. RETURNS -1 IF FOUND, 0 IF NOT.!
@^UW? 1^Q,:SAU1 Q1"U L2R' Q1"S R' Q1?
!MACRO C: CAPITALIZE THE NEXT WORD.!
@^UC? MW"S 0AU1 D Q1&95I S R'?
!MACRO U: SET WORD TO UPPER CASE.!
@^UU? MW"S !A! 0AU1 Q1"A D Q1&95I OA''?
!MACRO D: SET WORD TO LOWER CASE.!
@^UD? MW"S !A! 0AU1 Q1"A D Q1#32I OA''?
!MACRO M: MAIN LEVEL MACRO TO PROCESS MODULE BOILER PLATE.!
@^UM? JSCOPYRIGHT 0LML 0LMC FS67LML 0L5<MC>
2LML 0LMC 2<LML> S.MC
3<LML> S.MC  LML S IN MU
2LMLMC 2<LML> -SBY 2<MC> LMC
L2<LML> -LMU SBY MU
SACP ML 0L3<MU> 2<MC>
SABSTRACT: 2LMI  ?
!MACRO I: INTERACTIVE COMMENT PROCESSING.!
@^UI? ETUT ET#8ET 0L ML
<!C! -1W UC
QC-96"G QC-32UC'
QC-13"E  L :N^EQK"U OEXIT' ML'
QC-26"E OEXIT'
QC-27"E OEXIT'
QC-32"E 0A-13"N C''
QC-9"E MW"S 1^Q,:SS"U L2R'''
QC-8"E .-QA"G R''
QC-^^\"E MW"S 0AU1 D Q1#32I''
QC-^^^"E MW"S 0AU1 D Q1&95I''
QC-^^<"E MU'
QC-^^>"E MD'
QC-^^C"E MC'
QC-^^."E 1^Q,:S."U L2R''
> !EXIT! QTET 
 ?
EI
