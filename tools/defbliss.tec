! DEFBLISS: TECO MACRO TO CONVERT LANGUAGE INDEPENDENT STRUCTURE
  DEFINITIONS INTO BLISS SOURCE (MACROS AS ARGS FOR BLOCK STRUCTURE
  FETCHES AND OTHER CONSTANTS). OPEN THE FILE AND LET RUN.
  Q-REGISTER USAGE (IMPORTANT ONES ONLY):
	N: STRUCTURE NAME
	O: DEFINITION NAME SUFFIX
	M: NAME OF CURRENT FIELD OR SUBFIELD
	V: ASCII UNIT SUFFIX
	C: NUMBER OF UNITS IN FIELD OR SUBFIELD
	W: TOTAL FIELD OR SUBFIELD WIDTH IN BITS
	S: STARTING BIT OFFSET OF CURRENT SUBFIELD
	T: TOP BIT POSITION OF CURRENT SUBFIELD
	U: TOP BIT POSITION OF LAST SUBFIELD
	F: STARTING BIT OFFSET OF CURRENT FIELD
	G: TOP BIT POSITION OF CURRENT FIELD
	I,J,K,L: NUMERICAL ARGS TO STUCTURE REF MACRO
!
@^US/<S -1A-^^_"N -1A-^^$"N 0;''>/
@^UE/<S-1A-^^!"E R0;' -1A-13"E R0;'>/
@^UR/ME0KIMACRO	GNI$QVII_GMI	= QI\I, QJ\I, QK\I,  QL\I%;	/
@^UX/0L<0A-^^!"E 0L0;' 0A-13"E 0L0;'
0A-^^<"E DI(R' 0A-^^>"E DI)R' 0A-^^@"E DI^R' 0A-^^\"E DI OR R'
0A-^^&"E DI AND R' ::S^C"S -2DI NOT R'
::S^B"S FR%B'SDRI'R' ::S^O"S FR%O'SDRI'R'
::S^X"S FR%X'SRI'R' C>/

1ED
J<FS!\;>
J<FS;!;>
J<S$STRUCT;SS.U1MSRQ1,.XN ^UODEF 0A-^^,"E C.U1MSR Q1,.XO' ME
0KI!...$GNGOI				 L
0UF 0UG 0US 32UT

<!LOOP! ::SS 0A-^^F"E SS.U1MSRQ1,.XM .-Q1U2 0UL1UC0UW^^LUV0A-^^,"E C0AUV'
QV-^^B"E 8UW' QV-^^T"E 8UW' QV-^^W"E 16UW' QV-^^L"E 32UW' QV-^^Q"E 64UW'
QW"E OBADW'
MS R0A-^^,"E C\UC QC"E 1UC' QW*QCUW 0A-^^,"E 1A-^^S"E 1UL'''
QG/8UI 0UJ 0UK QW-33"L QWUK' Q2"N MR L
QW-32"G ILITERAL	GNI$S_GMI	= QW/8\I;		! FIELD SIZE IN BYTES
'' Q2"E ME0KI					C-1A-^^!"N RI!'I QW/8\I BYTES  QW/8-1"E -D'L'
QGUF QF+QWUG QFUS QGUT QFUU OLOOP'

0A-^^V"E S<0AUM ME0A-^^!"E 0KI					 LOA' 0LK !A! 0UB ^^VUV
<!L!QMUC ::SS  0A-^^>"E 0KDI		 ME0A-^^!"N 0LK-L'L0;'
0A-13"E LOL' 0A-^^!"E LOL'
.U1MSRQ1,.XM .-Q1U2 0UK0UL 0A-^^,"E C\UK 0A-^^,"E C0A-^^S"E 1ULC'
0A-^^,"E 1AUC''' QK"E 1UK'
QS+QB+QK-QT"G OBADV' QS/8UI QBUJ Q2"N MR L
QC-^^M"E ILITERAL	GNI$M_GMI	= 1^QJ+QK\I - 1^QJ\
I;	! FIELD MASK
''
Q2"E ME0KI					C-1A-^^!"N RI!'I QK\I BITS QK-1"E -D'L' QB+QKUB> OLOOP'

0A-^^S"E SS.U1MSRQ1,.XM .-Q1U2 0UL1UC0UW^^LUV
QUUS0A-^^,"E C.U1\U3 .-Q1"N Q3*8+QFUS'' 0A-^^,"E C0AUV'
QV-^^B"E 8UW' QV-^^T"E 8UW' QV-^^W"E 16UW' QV-^^L"E 32UW' QV-^^Q"E 64UW'
QW"E OBADW'
MS R0A-^^,"E C\UC QC"E 1UC' QW*QCUW 0A-^^,"E 1A-^^S"E 1UL'''
!EOL! QS+QW-QG"G OBADS' QS/8UI 0UJ 0UK QW-33"L QWUK' Q2"N MR L
QW-32"G ILITERAL	GNI$S_GMI	= QW/8\I;		! FIELD SIZE IN BYTES
'' Q2"E ME0KI					C-1A-^^!"N RI!'I QW/8\I BYTES  QW/8-1"E -D'L'
QS+QWUT QTUU OLOOP'

0A-^^C"E C::SS 0A-^^<"N .U1MSRQ1,.XM ^^CUV S,0KILITERAL	GNI$QVII_GMI	=  MX
0LME<-1A-32"N -1A-9"N 0;'' R> I;L OLOOP'

0U70U9C ::SS .U1MS [N R.-Q1"N Q1,.XN'
^UV$C_ 0A-^^,"E C.U1MSR .-Q1"N Q1,.XV'' 0KILITERAL
$EQULST	(GNGVI,GBL0A-^^,"N I,R' C.U1MSR .-Q1"E I0'
0A-^^,"N I,R' C.U1MSR .-Q1"E I1' Q1,.XW .U1ME Q1,.KI,	L
<!L! ::SS .U1 0A-^^>"E 0KDI		); Q9"N Q9JDL' L0;'
0A-13"E LOL' 0A-^^!"E LOL'
MXQ1J Q7"E I(' ME<-1A-32"N -1A-9"N 0;'' R> .+1U9
-1A-^^-"E -D1U7 LOL'I),	 L > ]N OLOOP'

0A-^^L"E SS.U1MSRQ1,.XM ^^CUV
ME0KILITERAL	GNI$QVII_GMI	= QG/8\I;		L OLOOP'

0A-^^E"E ME0KI					L0;'
L>
> OEXIT!BADW! BAD FIELD TYPE CHARACTER
 V OEXIT!BADV! VIELD EXCEEDS SURROUDING FIELD
 V OEXIT!BADS! SUBFIELD EXCEEDS FIELD
 V OEXIT!EXIT! EI
