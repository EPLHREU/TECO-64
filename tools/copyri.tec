!MACRO TO UPDATE COPYRIGHT STATEMENT IN ALL FILES IN A DIRECTORY
LISTING. LOAD MACRO INTO QA, OPEN THE DIRECTORY FILE FOR READ, AND
LET FLY.!
!MACRO TO CREATE NEW COPYRIGHT STATEMENT. EXITS IF CORRECT STATEMENT
EXISTS, FORMS NEW STATEMENT OTHERWISE FROM PREVIOUS STATEMENT.
COMPLAINS IF THERE IS NO STATEMENT AT ALL!

@^UX# 0UC !Y!Z"EAOY'
:SCOPYRIGHT"S0L.U1 :S(C)"S0L.-Q1"E%C :S1975"S0L.-Q1"EOOK'''''
J:SCOPYRIGHT"UONONE' QC"EI (C)' 0L.U1 :S197"UONONE' 0L.-Q1"NONONE'
 * MW 0LX9G9-S197 0A-^^5"E 0LK EC OOK' DI5 EC OOK
!NONE!  NO COPYRIGHT !OK!
#
!OUTER MACRO FOR COPYRIGHT UPDATING. TAKES A /BR DIRECTORY LISTING AND
 CONVERTS IT INTO COMMANDS TO PROCESS EACH FILE.!

EWCONTRL.TMP;1
HKJ<!LOOP! Z"E ^N^_; Y 0A-12"E D''
.U1 LQ1+3-."G -KOLOOP' -L
::SDIRECTORY "S 0K L2R0XD 0LK OLOOP'
L2R0X9 0L 1I I
 GD L2R 1IL
I@^UW?EW GDG9 27I I?
ER GDG9 27I IYMX
 12I 0,.PW 0,.K>
EF

ERCONTRL.TMP;1EP
<ER YEP -Z; HXZMZ>
EWCONTRL.TMP;1EK
^A

