! TECO macro to count lines of code in MACRO or BLISS. Expects result
  disk directory layout on default device.

  Q-register usage:

	QW = language:
	   = 0 for unknown
	   = 1 for Macro
	   = 2 for Bliss
	   = 3 for C
	QA = number of MACRO modules
	QB = number of MACRO code lines
	QC = number of MACRO comment lines
	QD = number of MACRO blank lines
	QE = number of BLISS modules
	QF = number of BLISS code lines
	QG = number of BLISS comment lines
	QH = number of BLISS blank lines
	QN = number of BLISS macro code lines
	QI = number of C modules
	QJ = number of C code lines
	QK = number of C comment lines
	QL = number of C blank lines
	QM = number of unknown modules
	Q1 = current line pointer
	Q2 = local page count
	Q3 = start of code seen
	Q4 = in comment
!
0ua 0ub 0uc 0ud 0ue 0uf 0ug 0uh 0ui 0uj 0uk 0ul 0um 0un
en[*.lis]*.lis

! Open next file !
<:en; erq* 0uw 0u1 0u2 0u3 0u4
! Determine language !
<:y; %2'
1:fb VAX-11 MACRO "s 1uw %a 0;'
1:fb	VAX-11 Bliss-32 "s 2uw %e 0;'
1:fb VAX C "s 3uw %i 0;'
q2-3"g %m 0;'>
! Macro !
qw-1"e
<jl::sTable of contents"s zj | ::sSymbol table"s 0; | j3l .u1''
 <.u1 .-z; l2r .-q1-47"g 0l 48c 0a-13"e %d | 0a-^^;"e %c | %b''' l>
:y; %2>'
! Bliss !
qw-2"e
<j3l
 <.u1 .-z; l2r .-q1-8"g 0l 0a-^^;"e 9c 0a-13"e %h | 0a-^^!"e %g |
  %f 1:fbeludom"s oeom''' | %n'' l>
:y; %2>'
! C !
qw-3"e
<j3l
 <.u1 .-z; ::sCommand Line"s oeom'
  l2r .-q1-13"g 0l14c 0a-13"e %l | q4"e ::s/*"u %j | %k'
  1:fb/*"s 1u4 .-q1-16"e %k'' | 1:fb*/"s 0u4 .u5 l2r.-q5"n %j | %k' | %k'''' l>
:y; %2>'
!eom!>
Number of MACRO modules		= QA = 
Number of MACRO code lines	= QB = 
Number of MACRO comment lines	= QC = 
Number of MACRO blank lines	= QD = 
Number of BLISS modules		= QE = 
Number of BLISS code lines	= QF = 
Number of BLISS comment lines	= QG = 
Number of BLISS blank lines	= QH = 
Number of BLISS macro code lines= QN = 
Number of C modules		= QI = 
Number of C code lines		= QJ = 
Number of C comment lines	= QK = 
Number of C blank lines		= QL = 
Number of unknown modules	= QM =
hkex
